use crate::lexer::*;
use crate::parser::*;

grammar<'a>;

pub ASTFile: ASTFile = {
    <fns: ASTFn*> => ASTFile { fns: fns }
};

pub ASTFn: ASTFn = {
    "fn" <name: ASTIdentifier> "(" <args: Comma<ASTIdentifier>> ")" <expr: ASTExpr> => ASTFn {
        decl: ASTFnDecl { name: name, args: args },
        body: expr
    }
};

pub ASTIdentifier: ASTIdentifier = {
    "id" => ASTIdentifier { name: <>.as_string() }
};

pub ASTExpr: ASTExpr = {
    <expr1: ASTExpr> "+" <expr2: SubExpr> => ASTExpr::Op(Opcode::Add, Box::new(expr1), Box::new(expr2)),
    <expr1: ASTExpr> "-" <expr2: SubExpr> => ASTExpr::Op(Opcode::Sub, Box::new(expr1), Box::new(expr2)),
    SubExpr => <>
}

pub SubExpr: ASTExpr = {
    <expr1: SubExpr> "*" <expr2: SubSubExpr> => ASTExpr::Op(Opcode::Mul, Box::new(expr1), Box::new(expr2)),
    SubSubExpr => <>
}

pub SubSubExpr: ASTExpr = {
    "num" => ASTExpr::Literal(<>.as_f64()),
    <fn_name: ASTIdentifier> "(" <fn_args: Comma<ASTExpr>> ")" => ASTExpr::FnCall(ASTFnCall {
        name: fn_name,
        args: fn_args
    }),
    ASTIdentifier => ASTExpr::Var(<>),
    "(" <expr: ASTExpr> ")" => expr
}

Comma<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

extern {
    type Location = usize;
    type Error    = usize;

    enum Token<'a> {
        "fn"    => Token::Fn,
        "("     => Token::OpenPar,
        ")"     => Token::ClosePar,
        ","     => Token::Comma,
        "+"     => Token::Operation(Opcode::Add),
        "-"     => Token::Operation(Opcode::Sub),
        "*"     => Token::Operation(Opcode::Mul),
        "num"   => Token::Num(_),
        "id"    => Token::Identifier(_)
    }
}
